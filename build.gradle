plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    // Apply the application plugin to add support for building a CLI application.
    id 'application'
    id 'checkstyle'
    id 'io.freefair.lombok' version '6.0.0-m2'
}

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

dependencies {
    checkstyle project(':checks')
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.pdfbox:pdfbox:2.0.24'
    // Use JUnit test framework
    testImplementation 'junit:junit:4.13'
    testImplementation "org.mockito:mockito-core:3.12.4"
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"


test {
    // display the following test events
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        exceptionFormat = 'full'
    }
}

application {
    mainClassName = 'edu.tec.ic6821.fulltextsearch.indexer.IndexerCmd'
    applicationName = 'indexar'
}

task createSearcherApp(type: CreateStartScripts) {
    mainClassName = 'edu.tec.ic6821.fulltextsearch.searcher.SearcherCmd'
    applicationName = 'buscar'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(createSearcherApp)
    fileMode = 0755
}

run {
    standardInput = System.in
    standardOutput = System.out
}
